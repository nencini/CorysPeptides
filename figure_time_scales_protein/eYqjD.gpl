set size 1,1

set terminal pdf enhanced size 6,4 #size in inches
set encoding utf8
set output "eYqjD_relax_times.pdf"
set datafile commentschar "@#$%&" 
 
############################
### SET RANGES and FORMATING - can be set also individually for each plot anain
############################
 
set yrange [0:1] 
set ytics 0.2 format "%.2f" font 'Times,12'  # sets frequancy of numbers and number of decimal places shown, in this case would be 0.00, 0.20, 0.40, 0.60, 0.80, 1.00
                                             # if not defined, gnuplot sets automatically, posible to set only size, for example, "set ytics font 'Times,12' "
 
set xrange [1:22] 
set xtics 1 format "%.0f" font 'Times,12' 

############################



############################
### SET KEY LEGEND
############################
 
set key inside right maxcols 1 font 'Times,18' 
set key reverse
set key Left

############################     



set multiplot layout 3,1


############################
### SET SUBPLOTS POSITIONING - probably has to be adjusted according to needs
############################
Left_margin=0.13
Right_margin=0.98

V1=0.97
V2=0.68
V3=0.39
V4=0.10

###########

Top_plot = "set tmargin at screen V1; set bmargin at screen V2+0.03; set lmargin at screen Left_margin; set rmargin at screen Right_margin"
Middle_plot = "set tmargin at screen V2-0.015; set bmargin at screen V3+0.015; set lmargin at screen Left_margin; set rmargin at screen Right_margin"
Bottom_plot = "set tmargin at screen V3-0.03; set bmargin at screen V4; set lmargin at screen Left_margin; set rmargin at screen Right_margin"

############################

@Top_plot

set yrange [0.6:1]

set ytics 0.1 format "%.2f" font 'Times,12'
unset xtics
#set xlabel "X label"  font 'Times,16'
set ylabel "T1 [s]"  font 'Times,16'
#set title "Title"  font 'Times,22' #can then use "unset title", or "unset xlabel"/ "unset ylabel", if you do not want these later on...

plot "experimental_relaxation_data/eYqjD_DSDS_T1_experiment.dat"  w linespoints lw 4 pt 5 lc rgb "black" title "Experiment",\
"eYqjD_SDS_micelle.out" u ($0+2):2 w linespoints lw 2 lc 1 title "Simulation",

@Middle_plot
set ytics 0.05 format "%.2f" font 'Times,12'
set yrange [0:0.25]
#set xlabel "X label"  font 'Times,16'
set ylabel "T2 [s]"  font 'Times,16'
#set title "Title"  font 'Times,22'

plot "eYqjD_SDS_micelle.out" u ($0+2):3 w linespoints lw 2 notitle, \
"experimental_relaxation_data/eYqjD_DSDS_T2_experiment.dat"  w linespoints lw 4 pt 5 lc rgb "black" notitle  # put your plot  # put your plot


@Bottom_plot
set ytics 0.2 format "%.2f" font 'Times,12'
set yrange [0:1]
set xtics format "%.0f" font 'Times,12' 
set xlabel "Residue"  font 'Times,16'
set ylabel "hetNOE [s]"  font 'Times,16'
#set title "Title"  font 'Times,22'

plot "eYqjD_SDS_micelle.out" u ($0+2):4 w linespoints lw 2 notitle, \
"experimental_relaxation_data/eYqjD_DSDS_hetnoe_experiment.dat"  w linespoints lw 4 pt 5 lc rgb "black" notitle  # put your plot  # put your plot
 

